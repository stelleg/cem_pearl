\begin{figure*}
\begin{code}
compile e = compile' 0 e where 
  compile' l e = case e of
    Var i -> [varBB l i]
    Lam b -> checkTermBB l 
           : termBB l 
           : checkUpdateBB l 
           : updateBB l 
           : takeBB l 
           : compile' (succ l) b
    App m n -> appBB l nlab : ms ++ ns where
      ms = compile' (succ l) m
      ns@((nlab,_):_) = compile' (succ l + length ms) n

type Location = Int

appBB :: Location -> Label -> BasicBlock
appBB l n = ("App_"++show l, 
  ["push %rax"                                      -- Push environment
  ,"push $"++n])                                    -- Push argument code

varBB :: Location -> Var -> BasicBlock
varBB l i = ("Var_"++show l,
  replicate i "movq 16(%rax), %rax"                 -- Index into environment
  ++ ["push %rax"                                   -- Push update location
     ,"push $0"                                     -- Push update marker
     ,"movq %rax, %rcx"                             -- \
     ,"movq 8(%rax), %rax"                          -- Load new Environment
     ,"jmp *(%rcx)"])                               -- Jump to new code

checkTermBB :: Location -> BasicBlock
checkTermBB l = ("CheckTerm_"++show l,
  ["cmp %rsp, %rbp"                                 -- Check if stack is empty
  ,"jne CheckUpdate_"++show l])                     -- If not empty, check updates

termBB :: Location -> BasicBlock
termBB l = ("Term_"++show l,
  ["movq $"++show l++", %rdi"                       -- \
  ,"movq $60, %rax"                                 -- Exit with label exitcode
  ,"syscall"])                                      -- / 

checkUpdateBB :: Location -> BasicBlock
checkUpdateBB l = ("CheckUpdate_"++show l,
  ["cmpq $0, (%rsp)"                                -- Check for update marker
  ,"jne Take_"++show l])                            -- If not update, proceed to take

updateBB :: Location -> BasicBlock
updateBB l = ("Update_"++show l,
  ["movq 8(%rsp), %rcx"                             -- \
  ,"movq $CheckTerm_"++show l++", "++"(%rcx)"       --  Replace code pointer
  ,"movq %rax, 8(%rcx)"                             --  Replace env pointer
  ,"add $16, %rsp"                                  --  Pop update 
  ,"jmp CheckTerm_"++show l])                       --  Continue with new stack
 
takeBB :: Location -> BasicBlock
takeBB l = ("Take_"++show l,
  ["pop (%rbx)"                                     -- Pop code into free heap cell
  ,"pop 8(%rbx)"                                    -- Pop env into free heap cell
  ,"movq %rax, 16(%rbx)"                            -- Point env to free heap cell
  ,"movq %rbx, %rax"                                -- /
  ,"add $24, %rbx"])                                -- Increment free heap cell

\end{code}
\caption{Full compiler implementation}
\label{fig:impl}
\end{figure*}
